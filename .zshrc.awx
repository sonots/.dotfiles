# aws switch
export AWX_CONFIG_DIR=$HOME/.awx-config
function awx-init() {
  profile="$1" # alias
  if [ -z "$1" ]; then
    echo "awx-init <profile>"
    return 1
  fi
  awx-activate "${profile}"
  aws configure
  ekx-init
}
function awx-activate() {
  profile="$1"
  export AWS_CONFIG_FILE="${AWX_CONFIG_DIR}/${profile}/config"
  export AWS_SHARED_CREDENTIALS_FILE="${AWX_CONFIG_DIR}/${profile}/credentials"
}
function awx-current() {
  local dirname="${AWS_CONFIG_FILE%/config}"
  echo "${dirname##*/}"
}
function awx-complete() {
  _values "awx-complete" $(\ls "${AWX_CONFIG_DIR}")
}
function awx() {
  profile="$1"
  if [ -z "${profile}" ]; then
    profile=$(\ls "${AWX_CONFIG_DIR}" | peco)
  fi
  if [ -z "${profile}" ]; then return 1; fi
  awx-activate "${profile}"
  ekx-activate-default
}
compdef awx-complete awx

# kubectl switch
export EKX_CONFIG_DIR=$HOME/.ekx-config
function ekx-init() {
  profile=$(awx-current)
  aws eks list-clusters | jq -r '.clusters[]' |  while read cluster; do
    ekx-activate "${profile}" "${cluster}"
    aws eks update-kubeconfig --name "${cluster}"
  done
}
function ekx-activate-default() {
  profile=$(awx-current)
  if [ -d "${EKX_CONFIG_DIR}/${profile}" ]; then
    cluster=$(\ls "${EKX_CONFIG_DIR}/${profile}" | head -n 1)
    export KUBECONFIG="${EKX_CONFIG_DIR}/${profile}/${cluster}"
  else
    unset KUBECONFIG
  fi
}
function ekx-activate() {
  profile="$1"
  cluster="$2"
  export KUBECONFIG="${EKX_CONFIG_DIR}/${profile}/${cluster}"
}
function ekx-current() {
  echo ${KUBECONFIG##*/}
}
function ekx-complete() {
  profile=$(awx-current)
  _values "ekx-complete" $(\ls "${EKX_CONFIG_DIR}/${profile}")
}
function ekx() {
  cluster="$1"
  profile=$(awx-current)
  if [ -z "${cluster}" ]; then
    cluster=$(\ls "${EKX_CONFIG_DIR}/${profile}" | peco)
  fi
  ekx-activate "${profile}" "${cluster}"
}
compdef ekx-complete ekx
